# ai_guard: å±é™ºã‚³ãƒãƒ³ãƒ‰ã®å®Ÿè¡Œå‰ã«ç¢ºèªã‚’æŒŸã¿ã€ãƒ­ã‚°ã‚’æ®‹ã™
# éå¯¾è©±ã‚·ã‚§ãƒ«ã§ã‚‚å¯èƒ½ãªã‚‰ãƒ€ã‚¤ã‚¢ãƒ­ã‚°ã‚’å‡ºã—ã€å¤±æ•—æ™‚ã®ã¿å®‰å…¨å´ã«å€’ã™
[[ -n ${AI_GUARD_LOADED:-} ]] && return
AI_GUARD_LOADED=1

# Detect whether this shell is driven by an AI tool (Codex/Claudeç­‰)ã€‚
_ai_guard_is_ai_session() {
  [[ "${AI_GUARD_FORCE_AI:-0}" == "1" ]] && return 0

  local pcmd gp pid gp_pid
  pid=${PPID:-0}
  pcmd=$(ps -o command= -p "$pid" 2>/dev/null || true)
  gp_pid=$(ps -p "$pid" -o ppid= 2>/dev/null | tr -d ' ')
  gp=$(ps -o command= -p "$gp_pid" 2>/dev/null || true)

  if echo "$pcmd" | grep -qiE 'codex|claude|anthropic|openai|aider'; then
    return 0
  fi
  if echo "$gp" | grep -qiE 'codex|claude|anthropic|openai|aider'; then
    return 0
  fi

  return 1
}

ai_extreme_confirm() {
  # xtraceãŒæœ‰åŠ¹ãªã‚·ã‚§ãƒ«ã§ã‚‚ã€ã“ã®é–¢æ•°å†…ã®ãƒˆãƒ¬ãƒ¼ã‚¹å‡ºåŠ›ã‚’æŠ‘æ­¢ã—ã¦ãƒã‚¤ã‚ºã‚’é˜²ã
  setopt localoptions noxtrace
  if [[ "${AI_GUARD_ACTIVE:-0}" == "1" ]]; then
    builtin command "$@"
    return $?
  fi
  local _ai_guard_prev_active="${AI_GUARD_ACTIVE:-0}"
  AI_GUARD_ACTIVE=1
  local cmd="$1"; shift
  local args=("$@")
  # dispatch å´ã§æ¡ä»¶åˆ¤å®šæ¸ˆã¿ã€‚ã“ã“ã§ã¯å¿…ãšç¢ºèªã‚’å‡ºã™å‰æã€‚
  local needs_prompt=1
  local log_file="$HOME/.ai_extreme_confirm.log"
  local log_ready=1

  if ! touch "$log_file" 2>/dev/null; then
    log_ready=0
    printf "âš ï¸ ãƒ­ã‚°ãƒ•ã‚¡ã‚¤ãƒ« %s ã‚’ä½œæˆã§ãã¾ã›ã‚“ã§ã—ãŸã€‚æ¨©é™ã‚’ç¢ºèªã—ã¦ãã ã•ã„ã€‚\n" "$log_file" >&2
  fi

  local cwd_short="" context_block="" context_for_log=""
  {
    local cwd shell_proc parent_proc tty_name tmux_info tmux_window_name tmux_window_index
    cwd="$(pwd -P 2>/dev/null || pwd)"
    shell_proc="$(ps -o comm= -p "$$" 2>/dev/null | tr -d '\n')"
    parent_proc="$(ps -o comm= -p "$PPID" 2>/dev/null | tr -d '\n')"
    tty_name="$(tty 2>/dev/null || true)"
    [[ -z "$tty_name" ]] && tty_name="(not a tty)"
    [[ -z "$shell_proc" ]] && shell_proc="(unknown)"
    [[ -z "$parent_proc" ]] && parent_proc="(unknown)"
    local base_context_block cwd_display
    # æœ«å°¾2éšå±¤ã‚’å¼·èª¿è¡¨ç¤ºï¼ˆä¾‹: /Users/kazuph/projects/myapp â†’ projects/myappï¼‰
    local parent_dir=$(dirname "$cwd")
    local last_two="${parent_dir##*/}/${cwd##*/}"
    [[ "$parent_dir" == "/" ]] && last_two="${cwd##*/}"
    [[ "$cwd" == "/" ]] && last_two="/"
    cwd_short="$last_two"

    if [[ "$cwd" == "$HOME" ]]; then
      cwd_display="~"
      cwd_short="~"
    elif [[ "$cwd" == "$HOME"/* ]]; then
      cwd_display="~/${cwd#"$HOME/"}"
    else
      cwd_display="$cwd"
    fi
    base_context_block=$'ğŸ“ '"$cwd_short"$'\n   ('"$cwd_display"$')\n- ã‚·ã‚§ãƒ«: '"$shell_proc"$'\n- è¦ªãƒ—ãƒ­ã‚»ã‚¹: '"$parent_proc"$'\n- TTY: '"$tty_name"
    context_for_log="[cwd:${cwd}] [shell:${shell_proc}] [ppid:${parent_proc}] [tty:${tty_name}]"

    # tmux æƒ…å ±ï¼ˆTMUX_PANE ãŒç¥–å…ˆãƒ—ãƒ­ã‚»ã‚¹ã® pane ã¨ä¸€è‡´ã™ã‚‹å ´åˆã®ã¿ï¼‰
    local tmux_force="${AI_GUARD_TMUX_FORCE:-}"

    local tmux_context_block=""
    if [[ -n "${TMUX_PANE:-}" ]] && builtin command -v tmux >/dev/null 2>&1; then
      local tmux_window_index tmux_window_name tmux_pane_id tmux_pane_title
      tmux_window_name=$(tmux display-message -p -t "${TMUX_PANE}" '#{window_name}' 2>/dev/null | tr -d '\n')
      tmux_window_index=$(tmux display-message -p -t "${TMUX_PANE}" '#{window_index}' 2>/dev/null | tr -d '\n')
      tmux_pane_id=$(tmux display-message -p -t "${TMUX_PANE}" '#{pane_id}' 2>/dev/null | tr -d '\n')
      tmux_pane_title=$(tmux display-message -p -t "${TMUX_PANE}" '#{pane_title}' 2>/dev/null | tr -d '\n')

      if [[ -n "$tmux_window_name" || -n "$tmux_window_index" || -n "$tmux_pane_id" ]]; then
        tmux_info="[tmux ${tmux_window_index:-?}-${tmux_pane_id:-?}] ${tmux_window_name:-(no-name)}"
        local tmux_title_block=""
        if [[ -n "$tmux_pane_title" ]]; then
          tmux_title_block=$'\n- tmux pane title: '"$tmux_pane_title"
        fi
        tmux_context_block=$'- tmux: '"$tmux_info"$tmux_title_block$'\n'
        context_for_log="[tmux:${tmux_info}${tmux_pane_title:+ |title:${tmux_pane_title}}] ${context_for_log}"
      fi
    fi

    # è¡¨ç¤ºç”¨ã‚³ãƒ³ãƒ†ã‚­ã‚¹ãƒˆã‚’çµ„ã¿ç«‹ã¦ï¼ˆğŸ“ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’æœ€ä¸Šéƒ¨ã«ï¼‰
    context_block="$base_context_block"$'\n'"$tmux_context_block"
  } >/dev/null

  local cmd_display cmd_display_for_prompt
  cmd_display="$(printf "%s " "$cmd" "${args[@]}")"
  cmd_display="${cmd_display% }"
  if [[ -n "${AI_GUARD_CMD_DISPLAY:-}" ]]; then
    cmd_display="${AI_GUARD_CMD_DISPLAY}"
  fi
  cmd_display="${cmd_display//$'\n'/ }"
  cmd_display_for_prompt=$'- ã‚³ãƒãƒ³ãƒ‰: '"$cmd_display"

  if (( needs_prompt )); then
    local dialog_output button_choice reason_text

    # GUIãƒ€ã‚¤ã‚¢ãƒ­ã‚°ã¯1å›ã ã‘è©¦è¡Œã—ã€å¤±æ•—ãƒ»å´ä¸‹ãªã‚‰å³ã‚­ãƒ£ãƒ³ã‚»ãƒ«æ‰±ã„
    # ï¼ˆã‚¹ã‚¯ãƒªãƒ—ãƒˆç­‰ã§GUIã‚’å‡ºã—ãŸããªã„å ´åˆ: AI_GUARD_NO_GUI=1ï¼‰
    if [[ "${AI_GUARD_NO_GUI:-0}" != "1" ]] && command -v osascript >/dev/null 2>&1; then
      local tmp_as
      tmp_as=$(mktemp -t ai_guard_dialog) || tmp_as=""
      if [[ -n "$tmp_as" ]]; then
        cat <<'APPLESCRIPT' >| "$tmp_as"
on run argv
  set cmdText to item 1 of argv
  set ctxText to item 2 of argv
  set titleText to item 3 of argv
  set promptText to "âš ï¸ æœ¬å½“ã«å®Ÿè¡Œã—ã¾ã™ã‹ï¼Ÿ" & return & cmdText & return & ctxText & return & return & "æ‰¿èª/å´ä¸‹ã®ç†ç”±ã‚’å…¥åŠ›ã—ã¦ãã ã•ã„ã€‚"
  try
    set resp to display dialog promptText default answer "" buttons {"å´ä¸‹", "æ‰¿èª"} default button "å´ä¸‹" with title titleText with icon stop
    return (button returned of resp) & linefeed & (text returned of resp)
  on error number -128
    return "ESC" & linefeed & ""
  end try
end run
APPLESCRIPT
        # ã‚¿ã‚¤ãƒˆãƒ«ã«ã€Œã‚³ãƒãƒ³ãƒ‰ @ ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªæœ«å°¾2éšå±¤ã€ã‚’è¡¨ç¤º
        local dialog_title="${cmd} @ ${cwd_short}"
        dialog_output=$(osascript "$tmp_as" "$cmd_display_for_prompt" "$context_block" "$dialog_title" 2>/dev/null) || dialog_output=""
        builtin command rm -f "$tmp_as"
      fi
    fi

    if [[ -n "$dialog_output" ]]; then
      button_choice="${dialog_output%%$'\n'*}"
      reason_text="${dialog_output#*$'\n'}"
    fi

    if [[ -z "$button_choice" ]]; then
      if [[ -t 0 && -t 1 ]]; then
        printf "âš ï¸ æœ¬å½“ã«å®Ÿè¡Œã—ã¾ã™ã‹ï¼Ÿ\n%s\n%s\næ‰¿èªã™ã‚‹å ´åˆã¯ y/yes ã‚’å…¥åŠ›ã—ã¦ãã ã•ã„ã€‚\nç†ç”±: " "$cmd_display_for_prompt" "$context_block" >&2
        read -r reason_text
        printf "æ‰¿èªã—ã¾ã™ã‹ï¼Ÿ [y/N]: " >&2
        read -r button_choice
      else
        reason_text="ãƒ€ã‚¤ã‚¢ãƒ­ã‚°è¡¨ç¤ºã«å¤±æ•— (osascript å¿œç­”ãªã—/TTYãªã—)ã€‚"
        local reason_clean reason_for_log
        reason_clean="${reason_text//$'\n'/ }"
        reason_clean="${reason_clean//$'\t'/ }"
        reason_for_log="${reason_clean:-æœªå…¥åŠ›} ${context_for_log}"
        printf "âŒ Command cancelled: %s\n   ç†ç”±: %s\n   %s\n" "$cmd_display" "$reason_text" "$context_block" >&2
        (( log_ready )) && printf "%s\tREJECT\t%s\t%s\n" "$(date -Iseconds)" "$cmd_display" "$reason_for_log" >> "$log_file"
        AI_GUARD_ACTIVE=${_ai_guard_prev_active}
        return 1
      fi
    fi

    local reason_clean log_reason
    reason_clean="${reason_text//$'\n'/ }"
    reason_clean="${reason_clean//$'\t'/ }"
    log_reason="${reason_clean:-æœªå…¥åŠ›} ${context_for_log}"

    if [[ "$button_choice" != "æ‰¿èª" && "$button_choice" != "y" && "$button_choice" != "yes" && "$button_choice" != "Y" ]]; then
      printf "âŒ Command cancelled: %s\n" "$cmd_display"
      printf "   ç†ç”±: %s\n" "${reason_text:-æœªå…¥åŠ›}"
      printf "   %s\n" "$context_block"
      (( log_ready )) && printf "%s\tREJECT\t%s\t%s\n" "$(date -Iseconds)" "$cmd_display" "$log_reason" >> "$log_file"
      AI_GUARD_ACTIVE=${_ai_guard_prev_active}
      return 1
    fi

    (( log_ready )) && printf "%s\tALLOW\t%s\t%s\n" "$(date -Iseconds)" "$cmd_display" "$log_reason" >> "$log_file"
    printf "âœ… æ‰¿èª: %s (ç†ç”±: %s)\n%s\n" "$cmd_display" "${reason_text:-æœªå…¥åŠ›}" "$context_block"
  fi

  if [[ -n "${AI_GUARD_EXEC:-}" ]]; then
    eval "$AI_GUARD_EXEC"
  else
    builtin command "$cmd" "${args[@]}"
  fi
  AI_GUARD_ACTIVE=${_ai_guard_prev_active}
}

alias sudo='sudo '

# ãƒ•ã‚¡ãƒ¼ãƒ ã‚¦ã‚§ã‚¢/ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£å‘¨ã‚Šã¯å¸¸ã«sudoã‚’è¦æ±‚
alias nvram='sudo nvram'
alias csrutil='sudo csrutil'
alias spctl='sudo spctl'

export PATH="/opt/homebrew/opt/trash/bin:$PATH"

# --- guard policy ------------------------------------------------------
# ç¢ºèªãƒ€ã‚¤ã‚¢ãƒ­ã‚°ã‚’å‡ºã™å¯¾è±¡ï¼š
# - å˜ä½“ã‚³ãƒãƒ³ãƒ‰: rm / rmdir / rimraf / trash / mv / dd / mkfs / fdisk / diskutil / format / parted / gparted
# - ã‚µãƒ–ã‚³ãƒãƒ³ãƒ‰: git reset|restore|checkout|clean|stash|branch|rebase|cherry-pick|merge
# - publish / deploy: å¼•æ•°ã®ã©ã“ã‹ã«å«ã¾ã‚Œã¦ã„ã‚Œã°å¸¸ã«ç¢ºèªï¼ˆnpx cdk deploy ç­‰ã‚‚æ¤œçŸ¥ï¼‰
# â€» git push ã¯ç¢ºèªä¸è¦
# â€» æ–°ã—ã„CLIãƒ„ãƒ¼ãƒ«ã‚’ä½¿ã†å ´åˆã¯ _AI_GUARD_TARGETS ã«è¿½åŠ ã—ã¦ãã ã•ã„

_AI_GUARD_TARGETS=(rm rmdir rimraf trash mv dd mkfs fdisk diskutil format parted gparted git gh sh bash zsh dash ksh fish nu aws npm npx pnpm pnpx yarn bun bunx deno cargo firebase vercel flyctl fly wrangler netlify railway render amplify cdk serverless sls pulumi terraform)

_AI_GUARD_DANGER_WORDS=(publish deploy put)
_AI_GUARD_DANGER_REGEX="(^|[^[:alnum:]])($(printf "%s|" "${_AI_GUARD_DANGER_WORDS[@]}" | sed 's/|$//'))([^[:alnum:]]|$)"

# è‡ªå‹•æ‰¿èªå¯¾è±¡ã®ãƒ‘ã‚¹ã‹ãƒã‚§ãƒƒã‚¯ï¼ˆ/tmp, /private/tmp, .artifacts/ ä»¥ä¸‹ï¼‰
# ãƒ‘ã‚¹ãƒˆãƒ©ãƒãƒ¼ã‚µãƒ«æ”»æ’ƒã‚’é˜²ããŸã‚ .. ã‚’å«ã‚€ãƒ‘ã‚¹ã¯æ‹’å¦
_ai_guard_is_safe_rm_path() {
  local target="$1"

  # ãƒ‘ã‚¹ãƒˆãƒ©ãƒãƒ¼ã‚µãƒ«ï¼ˆ..ï¼‰ã‚’å«ã‚€å ´åˆã¯å®‰å…¨ã§ãªã„ã¨ã¿ãªã™
  [[ "$target" == *..* ]] && return 1

  # çµ¶å¯¾ãƒ‘ã‚¹ã«å¤‰æ›
  local abs_path
  if [[ "$target" == /* ]]; then
    abs_path="$target"
  else
    abs_path="$(pwd -P)/$target"
  fi

  # /tmp ã¾ãŸã¯ /private/tmp ä»¥ä¸‹
  [[ "$abs_path" == /tmp/* || "$abs_path" == /private/tmp/* || "$abs_path" == /tmp || "$abs_path" == /private/tmp ]] && return 0

  # .artifacts/ ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªå†…
  [[ "$abs_path" == */.artifacts/* || "$abs_path" == */.artifacts ]] && return 0

  return 1
}

# rmç³»ã‚³ãƒãƒ³ãƒ‰ã®å…¨å¼•æ•°ãŒå®‰å…¨ãªãƒ‘ã‚¹ã‹ãƒã‚§ãƒƒã‚¯
_ai_guard_all_rm_paths_safe() {
  local arg
  for arg in "$@"; do
    # ã‚ªãƒ—ã‚·ãƒ§ãƒ³ï¼ˆ-ã§å§‹ã¾ã‚‹ï¼‰ã¯ã‚¹ã‚­ãƒƒãƒ—
    [[ "$arg" == -* ]] && continue
    # 1ã¤ã§ã‚‚å®‰å…¨ã§ãªã„ãƒ‘ã‚¹ãŒã‚ã‚Œã°ç¢ºèªãŒå¿…è¦
    _ai_guard_is_safe_rm_path "$arg" || return 1
  done
  return 0
}

_ai_guard_contains_danger_word() {
  local cmd_line="$*"
  [[ -n "$cmd_line" ]] || return 1
  local cmd_line_l="${cmd_line:l}"
  [[ "$cmd_line_l" =~ $_AI_GUARD_DANGER_REGEX ]] && return 0
  return 1
}

AI_GUARD_BLOCK_REASON=""
AI_GUARD_GIT_PUSH_DECISION=""
AI_GUARD_GH_PR_CREATE_DECISION=""
AI_GUARD_DANGER_WORD_ACK="0"
AI_GUARD_TRAP_ACTIVE="0"

_ai_guard_accept_line() {
  local cmd_line="$BUFFER"
  local cmd_trim="${cmd_line##[[:space:]]#}"
  local cmd_name="${cmd_trim%% *}"
  if [[ "$cmd_name" == "git" ]]; then
    zle .accept-line
    return 0
  fi
  if _ai_guard_contains_danger_word "$cmd_line"; then
    local prev_exec="${AI_GUARD_EXEC:-}"
    local prev_display="${AI_GUARD_CMD_DISPLAY:-}"
    AI_GUARD_EXEC=":"
    AI_GUARD_CMD_DISPLAY="$cmd_line"
    ai_extreme_confirm :
    local rc=$?
    AI_GUARD_EXEC="$prev_exec"
    AI_GUARD_CMD_DISPLAY="$prev_display"
    if [[ $rc -ne 0 ]]; then
      zle redisplay
      return 0
    fi
  fi
  zle .accept-line
}

if [[ -n "${ZSH_VERSION:-}" && -o interactive ]]; then
  zle -N accept-line _ai_guard_accept_line
fi

ai_guard_block() {
  local cmd_display="$1"
  local reason="$2"
  [[ -n "$reason" ]] || reason="ç†ç”±ä¸æ˜"
  printf "âŒ ãƒ–ãƒ­ãƒƒã‚¯: %s\n" "$cmd_display" >&2
  printf "ç†ç”±: %s\n" "$reason" >&2
  printf "æœ¬å½“ã«å¿…è¦ãªã‚‰ãƒ¦ãƒ¼ã‚¶ãƒ¼ã«ä¾é ¼ã—ã¦ãã ã•ã„ã€‚\n" >&2
}

_ai_guard_extract_github_repo_full() {
  local url="$1"
  local repo_full=""
  [[ -n "$url" ]] || return 1
  if [[ "$url" == *github.com* ]]; then
    repo_full=$(printf "%s" "$url" | sed -E 's#.*github\.com[:/]+([^/]+/[^/]+)(\\.git)?#\\1#')
    if [[ -n "$repo_full" && "$repo_full" != "$url" ]]; then
      printf "%s" "$repo_full"
      return 0
    fi
  fi
  return 1
}

_ai_guard_resolve_repo_full() {
  local remote_url="$1"
  local repo_full=""
  [[ -n "$remote_url" ]] || return 1

  repo_full=$(builtin command gh repo view "$remote_url" --json nameWithOwner --jq '.nameWithOwner' 2>/dev/null) || repo_full=""
  if [[ -n "$repo_full" ]]; then
    printf "%s" "$repo_full"
    return 0
  fi

  repo_full=$(_ai_guard_extract_github_repo_full "$remote_url") || repo_full=""
  if [[ -n "$repo_full" ]]; then
    printf "%s" "$repo_full"
    return 0
  fi

  return 1
}

_ai_guard_eval_git_push() {
  local subcmd="$1"; shift
  [[ "$subcmd" == "push" ]] || return 1

  AI_GUARD_BLOCK_REASON=""
  AI_GUARD_GIT_PUSH_DECISION="allow"

  # .allow-main ãƒ•ã‚¡ã‚¤ãƒ«ãŒå­˜åœ¨ã™ã‚‹å ´åˆã¯ main/master ã¸ã® push ã‚’è¨±å¯
  local git_root allow_main_flag=0
  git_root=$(builtin command git rev-parse --show-toplevel 2>/dev/null)
  if [[ -n "$git_root" && -f "${git_root}/.allow-main" ]]; then
    allow_main_flag=1
  fi

  local arg remote_name="" remote_name_set=0
  for arg in "$@"; do
    case "$arg" in
      --force|-f)
        # --force ã¯ .allow-main ãŒã‚ã£ã¦ã‚‚ç¢ºèªãŒå¿…è¦
        AI_GUARD_BLOCK_REASON="--force/-f ã¯ç¢ºèªãŒå¿…è¦ã§ã™ã€‚"
        AI_GUARD_GIT_PUSH_DECISION="prompt"
        return 0
        ;;
      main|*/main|*:main|master|*/master|*:master)
        # .allow-main ãŒã‚ã‚‹å ´åˆã¯è¨±å¯ã€ãªã‘ã‚Œã°ãƒ–ãƒ­ãƒƒã‚¯
        if [[ "$allow_main_flag" -eq 0 ]]; then
          AI_GUARD_BLOCK_REASON="main/master ã¯ç¦æ­¢ã§ã™ã€‚è¨±å¯ã™ã‚‹ã«ã¯ãƒªãƒã‚¸ãƒˆãƒªãƒ«ãƒ¼ãƒˆã« .allow-main ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã¦ãã ã•ã„ã€‚"
          AI_GUARD_GIT_PUSH_DECISION="block"
          return 0
        fi
        ;;
      --force-with-lease)
        AI_GUARD_BLOCK_REASON="--force-with-lease ã¯ç¢ºèªãŒå¿…è¦ã§ã™ã€‚"
        AI_GUARD_GIT_PUSH_DECISION="prompt"
        return 0
        ;;
    esac
    if [[ "$arg" != -* && "$remote_name_set" -eq 0 ]]; then
      remote_name="$arg"
      remote_name_set=1
    fi
  done

  [[ -n "$remote_name" ]] || remote_name="origin"

  local remote_url repo_full owner my_login owner_type push_ok permission
  remote_url=$(builtin command git remote get-url "$remote_name" 2>/dev/null) || {
    AI_GUARD_GIT_PUSH_DECISION="prompt"
    return 0
  }
  repo_full=$(_ai_guard_resolve_repo_full "$remote_url") || repo_full=""
  if [[ -z "$repo_full" ]]; then
    AI_GUARD_GIT_PUSH_DECISION="prompt"
    return 0
  fi
  owner="${repo_full%%/*}"
  my_login=$(builtin command gh api /user --jq '.login' 2>/dev/null) || my_login=""
  if [[ -z "$my_login" ]]; then
    AI_GUARD_GIT_PUSH_DECISION="prompt"
    return 0
  fi

  if [[ "$owner" != "$my_login" ]]; then
    owner_type=$(builtin command gh api "repos/${repo_full}" --jq '.owner.type' 2>/dev/null) || owner_type=""
    push_ok=$(builtin command gh api "repos/${repo_full}" --jq '.permissions.push' 2>/dev/null) || push_ok=""
    if [[ -z "$owner_type" ]]; then
      AI_GUARD_GIT_PUSH_DECISION="prompt"
      return 0
    fi
    if [[ "$owner_type" == "Organization" ]]; then
      if [[ "$push_ok" == "true" ]]; then
        AI_GUARD_GIT_PUSH_DECISION="allow"
        return 0
      fi
      if [[ "$push_ok" == "false" ]]; then
        AI_GUARD_BLOCK_REASON="çµ„ç¹” (${owner}) ã¸ã® push æ¨©é™ãŒãªã„ãŸã‚ç¦æ­¢ã§ã™ã€‚"
        AI_GUARD_GIT_PUSH_DECISION="block"
        return 0
      fi
      permission=$(builtin command gh api "repos/${repo_full}/collaborators/${my_login}/permission" --jq '.permission' 2>/dev/null) || permission=""
      case "$permission" in
        admin|maintain|write)
          AI_GUARD_GIT_PUSH_DECISION="allow"
          return 0
          ;;
        read|triage)
          AI_GUARD_BLOCK_REASON="çµ„ç¹” (${owner}) ã¸ã® push æ¨©é™ãŒãªã„ãŸã‚ç¦æ­¢ã§ã™ã€‚"
          AI_GUARD_GIT_PUSH_DECISION="block"
          return 0
          ;;
      esac
      AI_GUARD_GIT_PUSH_DECISION="allow"
      return 0
    fi
    AI_GUARD_BLOCK_REASON="éæ‰€æœ‰ãƒªãƒã‚¸ãƒˆãƒª (${owner}) ã¸ã® push ã¯ç¦æ­¢ã§ã™ã€‚"
    AI_GUARD_GIT_PUSH_DECISION="block"
    return 0
  fi

  AI_GUARD_GIT_PUSH_DECISION="allow"
  return 0
}

_ai_guard_eval_gh_pr_create() {
  local subcmd="$1"; shift
  [[ "$subcmd" == "pr" && "$1" == "create" ]] || return 1
  shift

  AI_GUARD_BLOCK_REASON=""
  AI_GUARD_GH_PR_CREATE_DECISION="allow"

  local target_repo="" head_ref="" arg prev=""
  for arg in "$@"; do
    if [[ "$prev" == "--repo" || "$prev" == "-R" ]]; then
      target_repo="$arg"
    elif [[ "$prev" == "--head" ]]; then
      head_ref="$arg"
    fi
    prev="$arg"
  done

  local is_fork parent_full parent_owner
  is_fork=$(builtin command gh repo view --json isFork --jq '.isFork' 2>/dev/null) || {
    AI_GUARD_GH_PR_CREATE_DECISION="prompt"
    return 0
  }

  if [[ "$is_fork" != "true" ]]; then
    AI_GUARD_GH_PR_CREATE_DECISION="allow"
    return 0
  fi

  parent_full=$(builtin command gh repo view --json parent --jq 'if .parent then (.parent.owner.login + "/" + .parent.name) else "" end' 2>/dev/null)
  parent_owner=$(builtin command gh repo view --json parent --jq 'if .parent then .parent.owner.login else "" end' 2>/dev/null)

  if [[ -z "$parent_full" || -z "$parent_owner" ]]; then
    AI_GUARD_GH_PR_CREATE_DECISION="prompt"
    return 0
  fi

  if [[ -z "$target_repo" || "$target_repo" == "$parent_full" ]]; then
    AI_GUARD_BLOCK_REASON="fork äº‹æ•…é˜²æ­¢ã®ãŸã‚ã€upstream ã¸ã® PR ä½œæˆã¯ç¦æ­¢ã§ã™ã€‚"
    AI_GUARD_GH_PR_CREATE_DECISION="block"
    return 0
  fi

  if [[ -n "$head_ref" && "$head_ref" == "$parent_owner:"* ]]; then
    AI_GUARD_BLOCK_REASON="fork äº‹æ•…é˜²æ­¢ã®ãŸã‚ã€upstream ã¸ã® PR ä½œæˆã¯ç¦æ­¢ã§ã™ã€‚"
    AI_GUARD_GH_PR_CREATE_DECISION="block"
    return 0
  fi

  AI_GUARD_GH_PR_CREATE_DECISION="allow"
  return 0
}

_ai_guard_is_target() {
  local cmd="$1"
  (( ${_AI_GUARD_TARGETS[(i)$cmd]} )) && return 0
  return 1
}

_ai_guard_need_prompt() {
  local cmd="$1"; shift
  local cmd_line="$cmd"
  if [[ "$#" -gt 0 ]]; then
    cmd_line="$cmd_line $*"
  fi

  if [[ "${AI_GUARD_DANGER_WORD_ACK:-0}" == "1" ]]; then
    return 1
  fi
  if _ai_guard_contains_danger_word "$cmd_line"; then
    return 0
  fi

  case "$cmd" in
    rmdir|rimraf|trash)
      # /tmp ã‚„ .artifacts/ ä»¥ä¸‹ã¯è‡ªå‹•æ‰¿èª
      _ai_guard_all_rm_paths_safe "$@" && return 1
      return 0
      ;;
    mv)
      # backup/bak ç³»ã®å˜èªãŒå«ã¾ã‚Œã‚‹å ´åˆã®ã¿ç¢ºèªï¼ˆä¸Šæ›¸ããƒãƒƒã‚¯ã‚¢ãƒƒãƒ—äº‹æ•…é˜²æ­¢ï¼‰
      local mv_args="$*"
      if [[ "$mv_args" =~ (backup|bak|\.bak|\.backup|_backup|_bak) ]]; then
        return 0
      fi
      return 1
      ;;
    dd|mkfs|fdisk|diskutil|format|parted|gparted) return 0 ;;
    git)
      # â€» git push ã¯åŸå‰‡ç¢ºèªä¸è¦ï¼ˆæ‰€æœ‰åˆ¤å®šä¸èƒ½æ™‚ã¯åˆ¥å‡¦ç†ã§ç¢ºèªï¼‰
      # ä»¥ä¸‹ã¯æœªã‚³ãƒŸãƒƒãƒˆã®å¤‰æ›´ã‚„ã‚³ãƒ¼ãƒ‰ã‚’å¤±ã†å¯èƒ½æ€§ãŒã‚ã‚‹ãŸã‚ç¢ºèªãŒå¿…è¦
      case "$1" in
        reset|restore) return 0 ;;  # å¤‰æ›´ã‚’å·»ãæˆ»ã™
        checkout)
          # git checkout <branch> ã¯è¨±å¯ã€git checkout <file> ã‚„ -- <file> ã¯ç¢ºèª
          # å¼•æ•°ãŒ2ã¤ä»¥ä¸Šã‚ã‚‹ã‹ã€-- ãŒå«ã¾ã‚Œã‚‹å ´åˆã¯ãƒ•ã‚¡ã‚¤ãƒ«å¾©å…ƒã®å¯èƒ½æ€§
          [[ $# -ge 2 || "$*" == *" -- "* || "$2" == -* ]] && return 0
          ;;
        clean) return 0 ;;  # è¿½è·¡ã•ã‚Œã¦ã„ãªã„ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å‰Šé™¤
        stash)
          # stash drop / stash clear ã¯ç¢ºèªãŒå¿…è¦
          [[ "$2" == drop || "$2" == clear ]] && return 0
          ;;
        branch)
          # git branch -D (å¼·åˆ¶å‰Šé™¤) ã¯ç¢ºèªãŒå¿…è¦
          [[ "$2" == -D || "$2" == -d || "$2" == --delete ]] && return 0
          ;;
        rebase) return 0 ;;  # å±¥æ­´ã‚’æ›¸ãæ›ãˆã‚‹
        cherry-pick)
          # cherry-pick --abort ã¯ç¢ºèªãŒå¿…è¦
          [[ "$2" == --abort ]] && return 0
          ;;
        merge)
          # merge --abort ã¯ç¢ºèªãŒå¿…è¦
          [[ "$2" == --abort ]] && return 0
          ;;
        worktree)
          # worktree remove ã¯å®‰å…¨ãªã®ã§ç¢ºèªä¸è¦
          return 1
          ;;
      esac
      ;;
    gh)
      case "$1" in
        pr)
          [[ "$2" == "merge" ]] && return 0
          ;;
        repo)
          if [[ "$2" == "edit" ]]; then
            [[ " $* " == *" --visibility "* || " $* " == *" --public "* || " $* " == *" --private "* ]] && return 0
          fi
          ;;
      esac
      ;;
  esac
  return 1
}

_ai_guard_dispatch() {
  local cmd="$1"; shift

  # rm ã¯ AI/Human é–¢ä¿‚ãªãå³æ™‚ãƒ–ãƒ­ãƒƒã‚¯ï¼ˆtrashã‚’ä½¿ç”¨ã™ã‚‹ã“ã¨ï¼‰
  if [[ "$cmd" == "rm" ]]; then
    # rmã‚ªãƒ—ã‚·ãƒ§ãƒ³ã‚’é™¤å»ã—ã¦ãƒ‘ã‚¹ã®ã¿æŠ½å‡º
    local trash_targets=()
    local arg
    for arg in "$@"; do
      # -ã§å§‹ã¾ã‚‹ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã¯ã‚¹ã‚­ãƒƒãƒ—
      [[ "$arg" == -* ]] && continue
      trash_targets+=("$arg")
    done

    printf "âŒ rm ã‚³ãƒãƒ³ãƒ‰ã¯ç¦æ­¢ã•ã‚Œã¦ã„ã¾ã™ã€‚\n" >&2
    printf "\n" >&2
    if [[ ${#trash_targets[@]} -gt 0 ]]; then
      printf "ğŸ“‹ ä»£ã‚ã‚Šã«ã“ã¡ã‚‰ã‚’ã‚³ãƒ”ãƒ¼ã—ã¦å®Ÿè¡Œ:\n" >&2
      printf "  trash %s\n" "${trash_targets[*]}" >&2
      printf "\n" >&2
    fi
    printf "ğŸ’¡ ã‚´ãƒŸç®±ã‹ã‚‰å¾©å…ƒ: Finder â†’ ã‚´ãƒŸç®± â†’ å³ã‚¯ãƒªãƒƒã‚¯ â†’ æˆ»ã™\n" >&2
    return 1
  fi

  # Humanã‚»ãƒƒã‚·ãƒ§ãƒ³ã§ã¯ã‚¬ãƒ¼ãƒ‰ã‚’é€šã•ãšå³å®Ÿè¡Œ
  if _ai_guard_is_ai_session; then :; else
    builtin command "$cmd" "$@"
    return $?
  fi
  if [[ "${AI_GUARD_ACTIVE:-0}" == "1" ]]; then
    builtin command "$cmd" "$@"
    return $?
  fi

  local cmd_display
  cmd_display="$(printf "%s " "$cmd" "$@")"
  cmd_display="${cmd_display% }"

  if [[ "$cmd" == "git" ]] && _ai_guard_eval_git_push "$@"; then
    case "$AI_GUARD_GIT_PUSH_DECISION" in
      block)
        ai_guard_block "$cmd_display" "$AI_GUARD_BLOCK_REASON"
        return 1
        ;;
      prompt)
        ai_extreme_confirm "$cmd" "$@"
        return $?
        ;;
    esac
  fi

  if [[ "$cmd" == "gh" ]] && _ai_guard_eval_gh_pr_create "$@"; then
    case "$AI_GUARD_GH_PR_CREATE_DECISION" in
      block)
        ai_guard_block "$cmd_display" "$AI_GUARD_BLOCK_REASON"
        return 1
        ;;
      prompt)
        ai_extreme_confirm "$cmd" "$@"
        return $?
        ;;
    esac
  fi

  if _ai_guard_need_prompt "$cmd" "$@"; then
    ai_extreme_confirm "$cmd" "$@"
  else
    builtin command "$cmd" "$@"
  fi
}

# å¯¾è±¡ã‚³ãƒãƒ³ãƒ‰ã¯ä¸€æ‹¬ã§ãƒ©ãƒƒãƒ—
for _ai_cmd in "${_AI_GUARD_TARGETS[@]}"; do
  eval "${_ai_cmd}() { _ai_guard_dispatch ${_ai_cmd} \"\$@\"; }"
done
unset _ai_cmd

# æ—¢å­˜ reset ã‚¨ã‚¤ãƒªã‚¢ã‚¹ã‚’ä¿è­·ä»˜ãã§å®Ÿè¡Œ
unalias reset 2>/dev/null
reset() {
  local exec_cmd="brake db:migrate:reset"
  AI_GUARD_EXEC="$exec_cmd" ai_extreme_confirm reset
}

# command çµŒç”±ã®ãƒã‚¤ãƒ‘ã‚¹ã‚‚æ•æ‰
command() {
  local cmd="$1"; shift
  if _ai_guard_is_ai_session; then :; else
    builtin command "$cmd" "$@"
    return $?
  fi
  # ã‚¬ãƒ¼ãƒ‰å†…éƒ¨ã‹ã‚‰ã®å‘¼ã³å‡ºã—ã¯ãƒã‚¤ãƒ‘ã‚¹ã—ã¦ç„¡é™ãƒ«ãƒ¼ãƒ—ã‚’é˜²ã
  if [[ "${AI_GUARD_ACTIVE:-0}" == "1" ]]; then
    builtin command "$cmd" "$@"
    return $?
  fi
  if _ai_guard_is_target "$cmd"; then
    _ai_guard_dispatch "$cmd" "$@"
  else
    builtin command "$cmd" "$@"
  fi
}
